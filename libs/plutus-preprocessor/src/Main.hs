{-# LANGUAGE DataKinds #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE TemplateHaskell #-}

-- | The 'main' function in this file writes a file
-- @libs\/cardano-ledger-core\/testlib\/Test\/Cardano\/Ledger\/Plutus\/Examples.hs@. When
-- this file is compiled it exports a bunch of Plutus Scripts.  Compiling that file does
-- not have any dependency on the plutus-plugin.  Instead this package
-- 'plutus-preprocessor' has that dependency, but one does not have to compile this
-- package to build the system.  If the plutus package changes, we will have to regenerate
-- the Examples.hs file.
-- To regenerate Examples.hs, on a machine that can depend upon plutus=plugin,
-- run 'cabal run plutus-preprocessor'
module Main where

import Cardano.Ledger.Plutus.Language (Language (..))
import Data.ByteString.Short (ShortByteString, unpack)
import Data.Foldable (forM_)
import Language.Haskell.TH
import qualified PlutusV1Scripts as PV1S
import qualified PlutusV3Scripts as PV3S
import ScriptSource
import System.IO

-- =============================================
-- how to display a preprocessed script

display :: Handle -> (Language -> ShortByteString) -> Q [Dec] -> String -> IO ()
display h scriptBytesFun code name = do
  xxx <- runQ code
  hPutStrLn h ("\n\n{- Preproceesed Plutus Script\n" ++ pprint xxx ++ "\n-}")
  hPutStr h $
    concat
      [ "\n"
      , name ++ " :: SLanguage l -> Plutus l\n"
      , name ++ " = Plutus . PlutusBinary . pack . \n  (\\case\n"
      ]
  forM_ [PlutusV1 .. PlutusV3] $ \lang -> do
    hPutStrLn h $ "    S" <> show lang <> " -> " <> show (unpack (scriptBytesFun lang))
  hPutStrLn h "  )"

manylines :: Show t => Handle -> Int -> [t] -> IO ()
manylines h n ts' = write (split ts')
  where
    split [] = []
    split ts = take n ts : split (drop n ts)
    write [] = pure ()
    write [ts] = hPutStrLn h (show ts ++ "]")
    write (ts : tss) = do
      hPutStr h (show ts ++ ",\n   ")
      write tss

-- ========================================================================
-- Generate the PlutusScripts.hs which does not depend on plutus-plugin.
-- write out the file header (module and imports), then 'display' the result
-- for each plutus script.

displayScripts :: Handle -> IO ()
displayScripts outh = do
  let
    scripts =
      [
        ( \case
            PlutusV1 -> PV1S.alwaysSucceedsNoDatumBytes
            PlutusV2 -> PV1S.alwaysSucceedsNoDatumBytes
            PlutusV3 -> PV3S.alwaysSucceedsNoDatumBytes
        , alwaysSucceedsNoDatumQ
        , "alwaysSucceedsNoDatum"
        )
      ,
        ( \case
            PlutusV1 -> PV1S.alwaysSucceedsWithDatumBytes
            PlutusV2 -> PV1S.alwaysSucceedsWithDatumBytes
            PlutusV3 -> PV3S.alwaysSucceedsWithDatumBytes
        , alwaysSucceedsWithDatumQ
        , "alwaysSucceedsWithDatum"
        )
      ,
        ( \case
            PlutusV1 -> PV1S.alwaysFailsNoDatumBytes
            PlutusV2 -> PV1S.alwaysFailsNoDatumBytes
            PlutusV3 -> PV3S.alwaysFailsNoDatumBytes
        , alwaysFailsNoDatumQ
        , "alwaysFailsNoDatum"
        )
      ,
        ( \case
            PlutusV1 -> PV1S.alwaysFailsWithDatumBytes
            PlutusV2 -> PV1S.alwaysFailsWithDatumBytes
            PlutusV3 -> PV3S.alwaysFailsWithDatumBytes
        , alwaysFailsWithDatumQ
        , "alwaysFailsWithDatum"
        )
      ,
        ( \case
            PlutusV1 -> PV1S.redeemerSameAsDatumBytes
            PlutusV2 -> PV1S.redeemerSameAsDatumBytes
            PlutusV3 -> PV3S.redeemerSameAsDatumBytes
        , redeemerSameAsDatumQ
        , "redeemerSameAsDatum"
        )
      ,
        ( \case
            PlutusV1 -> PV1S.evenDatumBytes
            PlutusV2 -> PV1S.evenDatumBytes
            PlutusV3 -> PV3S.evenDatumBytes
        , evenDatumQ
        , "evenDatum"
        )
      ,
        ( \case
            PlutusV1 -> PV1S.evenRedeemerNoDatumBytes
            PlutusV2 -> PV1S.evenRedeemerNoDatumBytes
            PlutusV3 -> PV3S.evenRedeemerNoDatumBytes
        , evenRedeemerNoDatumQ
        , "evenRedeemerNoDatum"
        )
      ,
        ( \case
            PlutusV1 -> PV1S.evenRedeemerWithDatumBytes
            PlutusV2 -> PV1S.evenRedeemerWithDatumBytes
            PlutusV3 -> PV3S.evenRedeemerWithDatumBytes
        , evenRedeemerWithDatumQ
        , "evenRedeemerWithDatum"
        )
      ]
  forM_ scripts $ \(scriptBytesFun, scriptArgs, name) -> do
    display outh scriptBytesFun scriptArgs name

main :: IO ()
main = do
  outh <- openFile "libs/cardano-ledger-core/testlib/Test/Cardano/Ledger/Plutus/Examples.hs" WriteMode
  hPutStrLn outh $
    unlines
      [ "{-# LANGUAGE DataKinds #-}"
      , "{-# LANGUAGE LambdaCase #-}"
      , "{-# LANGUAGE GADTs #-}"
      , "-- | This file is generated by \"plutus-preprocessor:plutus-preprocessor\""
      , "module Test.Cardano.Ledger.Plutus.Examples where"
      , ""
      , "import Cardano.Ledger.Plutus.Language (SLanguage (..), Plutus (..), PlutusBinary (..))"
      , "import Data.ByteString.Short (pack)"
      ]
  displayScripts outh
  hClose outh
